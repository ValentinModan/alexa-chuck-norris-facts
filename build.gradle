import com.amazonaws.services.lambda.model.Runtime
import jp.classmethod.aws.gradle.lambda.AWSLambdaMigrateFunctionTask

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "jp.classmethod.aws.lambda" version "0.38"
    id "groovy"
}

group = 'chuck-norris-facts'
version = '1.0'

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8
}

repositories {
    jcenter()
}

dependencies {
    compile "com.amazon.alexa:ask-sdk:$alexasdkVersion"
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile "org.codehaus.groovy:groovy-json:$groovyVersion"

    runtime "com.amazonaws:aws-lambda-java-log4j2:1.0.0"
    runtime "org.apache.logging.log4j:log4j-slf4j-impl:2.9.1"
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

aws {
    region = 'eu-west-1'
}

shadowJar {
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
}

if (new File("${System.getProperty("user.home")}/.aws/credentials").exists()) {
    task deploy(type: AWSLambdaMigrateFunctionTask, dependsOn: build) {
        functionName = "alexa-chuck-norris-facts"
        handler = "chucknorrisfacts.ChuckStreamHandler"
        role = "arn:aws:iam::${aws.accountId}:role/lambda_basic_execution"
        runtime = Runtime.Java8
        zipFile = shadowJar.archivePath
        memorySize = 512
        timeout = 60
    }
}

build.dependsOn shadowJar
